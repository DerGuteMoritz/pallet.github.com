---
title: Scripts
layout: doc
permalink: /doc/reference/script
section: documentation
subsection: reference
summary: Pallet uses a script DSL called stevdeore to generate shell scripts
         that can target different distributions and operating systems.
---

Pallet scripts are written using an embedding of shell script in [Clojure](http://clojure.org), and
allows for abstraction of operating system variations.

At it's simplest, the `script` macro takes one or more forms and converts them
to shell script.

{% highlight clojure %}
  (require
    '[pallet.stevedore :as stevedore])

  (stevedore/script
    ("ls"))

  results in:

    ls
{% endhighlight %}

## Variables
There is an obvious mismatch between Clojure's immutable data approach, and
shell script's mutable variables.

### Substitution
For accessing the value of a script variable, stevedore uses `deref` or `@`, which
are also used for command substitution.

{% highlight clojure %}
  (stevedore/script
   (deref TMPDIR))

  results in:

    ${TMPDIR}
{% endhighlight %}

If the variable isn't set, a default can be supplied.
{% highlight clojure %}
  (stevedore/script
   @TMPDIR-/tmp)

  results in:

    ${TMPDIR-/tmp}
{% endhighlight %}


### Assignment
Values can be defined with `set!`.
{% highlight clojure %}
  (stevedore/script
   (set! x 1))

  results in:

    x=1
{% endhighlight %}


## Command Substitution

Similar to variable substitution, command substitution uses `deref`.

{% highlight clojure %}
  (stevedore/script
   (set! R @(cat "/etc/redhat-release")))

  results in:

    R=$(cat /etc/redhat-release)
{% endhighlight %}

## Loops
Shell script `for` loops are mapped to clojure's `doseq`.

{% highlight clojure %}
  (stevedore/script
   (doseq [x ["a" "b" "c"]]
     (println @x)))

  results in:

    for x in a b c; do
      echo ${x}
    done
{% endhighlight %}

## Functions

Calling a function follows clojure's normal syntax.

{% highlight clojure %}
  (stevedore/script
   (foo x y))

  results in:

    foo x y
{% endhighlight %}

Defining a function follows a simplified version of clojure's `defn`, and does
not allow for destructuring. Arguments are named and automatically mapped to the
shell function's numbered arguments.

{% highlight clojure %}
  (stevedore/script
   (script (defn foo [x y] (bar x))))

  results in:

    script function foo() {
      x=$1
      y=$2
      bar x
    }
{% endhighlight %}

## Arrays

Array literals are generated from clojure's `vector` notation.

{% highlight clojure %}
  (stevedore/script
   [1 "2" (quoted 3) :four])

  results in:
    (1 2 "3" four)
{% endhighlight %}

Array access uses `aget` and `aset`.

{% highlight clojure %}
  (stevedore/script
   (aget foo 1))

  results in:

    ${foo[1]}
{% endhighlight %}

{% highlight clojure %}
  (stevedore/script
   (aset foo 1 :foo))

  results in:

    foo[1]=foo
{% endhighlight %}

## Unquoting

One advantage of embedding script in clojure is that we can escape back to
clojure to substitute in values.

{% highlight clojure %}
  (let [tmp "/tmp"]
    (stevedore/script
      (ls ~tmp)))

  results in:

    ls /tmp
{% endhighlight %}

### Expressions
We can also unquote arbitrary clojure expressions.
{% highlight clojure %}
  (stevedore/script
   (ls ~(str "/" "tmp")))

  results in:

    ls /tmp
{% endhighlight %}

### Splicing
Sometimes we want to add the contents of a collection into a script, without
demarking the boundaries of the collection.
{% highlight clojure %}
  (let [files ["a" "b" "c"]]
    (stevedore/script
      (ls ~@files)))

  results in:

    ls a b c
{% endhighlight %}

## Script functions

Script functions are defined with `pallet.script/defscript` and provide multimethods
for shell script generation.  In pallet this is used to abstract away script
differences between operating systems.

{% highlight clojure %}
  (require
    '[pallet.stevedore :as stevedore]
    '[pallet.script :as script])

  (script/defscript ls [& args])
  (stevedore/defimpl ls :default [& args]
    (ls ~@args))
  (stevedore/defimpl ls [:windows] [& args]
    (dir ~@args))

  (script/with-template [:windows]
    (stevedore/script
      (ls a b c)))

  results in:

    dir a b c
{% endhighlight %}

## Scripting Guide

An excellent reference on shell scripting is available at
[http://tldp.org/LDP/abs/html/](http://tldp.org/LDP/abs/html/).
